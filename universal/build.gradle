plugins {
    id "com.modrinth.minotaur" version "2.4.4"
    id "io.github.CDAGaming.cursegradle" version "1.6.0"
}

minecraft {
    // N/A
}

remapJar {
    targetNamespace = "named"
}

def archive_name = "${rootProject.mod_name}-${rootProject.loader_name}"

curseforge {
    project {
        apiKey = System.getenv("CF_APIKEY")
        id = "297038"
        changelog = file("$rootDir/Changes.md")
        changelogType = "markdown"
        releaseType = "${deploymentType}".toLowerCase()
        def mcVersionTarget = "${mc_version}"
        for (String loaderName : rootProject.enabled_platforms.split(",")) {
            def loaderToAdd = loaderName

            // CurseForge Compatibility Layer (For Modloader and Beta MC)
            // (This can be removed when/if CurseForge ever adds support for this)
            if (isLegacy) {
                if ((protocol == 22 || protocol == 23) && loaderToAdd.equalsIgnoreCase("ModLoader")) { // MC 1.0.0 and 1.1.0
                    loaderToAdd = "Forge"
                } else if (protocol < 22) {
                    loaderToAdd = ""
                    mcVersionTarget = "1.0"
                }
            }
            addGameVersion loaderToAdd.capitalize()
        }
        addGameVersion mcVersionTarget

        mainArtifact(file("$rootDir/build/libs/${fileFormat}-universal.jar")) {
            it.displayName = "${archive_name} v${versionId} ${versionLabel} (${mc_version})"
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${mod_name}".toLowerCase()
    versionNumber = "v${versionFormat}_${loader_name}".replaceAll("\\s", "").toLowerCase()
    versionName = "${archive_name} v${versionId} ${versionLabel} (${mc_version})"
    changelog = file("$rootDir/Changes.md").text
    versionType = "${deploymentType}".toLowerCase()
    uploadFile = file("$rootDir/build/libs/${fileFormat}-universal.jar")
    gameVersions = ["${mc_version}"]
    loaders = rootProject.enabled_platforms.split(",").toList()
    dependencies = []
}

task publishToModSites {
    publishToModSites.dependsOn 'modrinth'
    publishToModSites.dependsOn 'curseforge'
}