plugins {
    id "com.modrinth.minotaur" version "2.4.4"
    id "io.github.CDAGaming.cursegradle" version "1.6.0"
}

def archive_name = "${rootProject.mod_name}-${rootProject.loader_name}"
def fileFormat = "${rootProject.mod_name}-${rootProject.mc_version}-${rootProject.deploymentType}-${rootProject.versionId}"

curseforge {
    project {
        apiKey = System.getenv("CF_APIKEY")
        id = "297038"
        changelog = file("$rootDir/Changes.md")
        changelogType = "markdown"
        releaseType = "${deploymentType}".toLowerCase()
        addGameVersion "1.19.3"
        for (String loaderName : rootProject.enabled_platforms.split(",")) {
            addGameVersion loaderName.capitalize()
        }

        mainArtifact(file("$rootDir/fabric/build/libs/${fileFormat}-fabric.jar")) {
            it.displayName = "${archive_name} v${versionId} ${versionLabel} (${mc_version})"
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${mod_name}".toLowerCase()
    versionNumber = "v${versionId}_${mc_version}-${loader_name}${versionLabel.equalsIgnoreCase("release") ? "" : "_${versionLabel}"}".replaceAll("\\s", "").toLowerCase()
    versionName = "${archive_name} v${versionId} ${versionLabel} (${mc_version})"
    changelog = file("$rootDir/Changes.md").text
    versionType = "${deploymentType}".toLowerCase()
    uploadFile = file("$rootDir/fabric/build/libs/${fileFormat}-fabric.jar")
    gameVersions = ["${mc_version}"]
    loaders = rootProject.enabled_platforms.split(",").toList()
    dependencies = []
}

task publishToModSites {
    publishToModSites.dependsOn 'modrinth'
    publishToModSites.dependsOn 'curseforge'
}