plugins {
    id 'java'
    id 'xyz.wagyourtail.unimined' version '0.1.2-SNAPSHOT'
    id "com.modrinth.minotaur" version "2.4.3"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "org.cadixdev.licenser" version "0.6.1"
}

import org.apache.tools.ant.filters.ReplaceTokens

def archive_name = "${mod_name}-${loader_name}"

version = "${versionId}"
group = "com.gitlab.cdagaming"
archivesBaseName = "${archive_name}-${mc_version}-${deploymentType}"

JavaVersion targetVersion = JavaVersion.VERSION_1_8

allprojects {
    sourceCompatibility = targetVersion
    targetCompatibility = targetVersion
}

license {
    // Exclude JUnixSocket Files
    exclude "**/newsclub/**"
    exclude "**/kohlschutter/**"
    exclude "**/maven/com.kohlschutter**"
    exclude "**/jni/**"
    // Exclude Non-Parsable Files
    exclude "**/*.lang"
    exclude "**/*.info"
    exclude "**/*.mcmeta"
    exclude "**/*.properties"
    // Exclude other External Sources
    exclude "**/WinRegistry.java"
    exclude "**/impl/guava/**"
}

curseforge {
    project {
        apiKey = System.getenv("CF_APIKEY")
        id = "297038"
        changelog = file("Changes.md")
        changelogType = "markdown"
        releaseType = "${deploymentType}".toLowerCase()
        addGameVersion "${mc_version}"
        addGameVersion "${loader_name}"

        mainArtifact(remapJar) {
            displayName = "${archive_name} v${version} ${versionLabel} (${mc_version})"
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${mod_name}".toLowerCase()
    versionNumber = "v${version}_${mc_version}-${loader_name}_${versionLabel}".replaceAll("\\s", "").toLowerCase()
    versionName = "${archive_name} v${version} ${versionLabel} (${mc_version})"
    changelog = file("Changes.md").text
    versionType = "${deploymentType}".toLowerCase()
    uploadFile = remapJar
    gameVersions = ["${mc_version}"]
    loaders = ["${loader_name}".toLowerCase()]
    dependencies = []
}

task publishToModSites {
    publishToModSites.dependsOn 'modrinth'
    publishToModSites.dependsOn 'curseforge'
}

unimined {
    // debug, puts some things in build/unimined instead of ~/.gradle/caches/unimined
    // I recommend you leave this off until unimined is stable
    useGlobalCache = true
}

minecraft {
    // current available options are: forge, jarMod, fabric
    // if you don't include this, it will default to no mod loader transforms
    switch ("${loader_name}".toLowerCase()) {
        case "forge":
            forge {
                // required for 1.7+
                it.mcpVersion = "${mc_mappings}"
                it.mcpChannel = 'stable'
            }
            break
        case "fabric":
            break
        default:
            jarMod()
            break
    }
    // required when using mcp mappings
    mcRemapper.fallbackTarget = "searge"

    mcRemapper.tinyRemapperConf = {
        // most mcp mappings (except older format) dont include field desc
        it.ignoreFieldDesc(true)
        // this also fixes some issues with them, as it tells tiny remapper to try harder to resolve conflicts
        it.ignoreConflicts(true)
    }
}

mappings {
    // ability to add custom mappings
    // available targets are "CLIENT", "SERVER", "COMBINED"
    getStub("COMBINED").withMappings(["searge", "named"]) {
        c("ModLoader", "ModLoader", "net/minecraft/src/ModLoader")
        c("BaseMod", "BaseMod", "net/minecraft/src/BaseMod")
        c("mod_${mod_name}", "mod_${mod_name}", "${group}".replace(".", "/") + "/${mod_name}".toLowerCase() + "/mod_${mod_name}")
    }
    getStub("CLIENT").withMappings(["searge", "named"]) {
        c("ModLoader", "ModLoader", "net/minecraft/src/ModLoader")
        c("BaseMod", "BaseMod", "net/minecraft/src/BaseMod")
        c("mod_${mod_name}", "mod_${mod_name}", "${group}".replace(".", "/") + "/${mod_name}".toLowerCase() + "/mod_${mod_name}")
    }

    addExport("CLIENT") {
        it.targetNamespace = ["named"]
        it.sourceNamespace = "official"
        it.location = file("$rootDir/src/main/resources/mappings.srg")
        it.setType "srg"
    }

    addExport("COMBINED") {
        it.targetNamespace = ["named"]
        it.sourceNamespace = "official"
        it.location = file("$rootDir/src/main/resources/mappings.srg")
        it.setType "srg"
    }
}

sourceSets {
    // enable the client configuration when not using combined (or mc <= 1.2.5)
    client
}


configurations {
    shadow
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    minecraft "net.minecraft:minecraft:${mc_version}"

    switch ("${loader_name}".toLowerCase()) {
        case "forge":
            forge "net.minecraftforge:forge:${mc_version}-${loader_version}"
            break
        case "fabric":
            break
        default:
            jarMod "local_mod:ModLoader:${loader_version}@zip"
            mappings "local_mod:mcp:${mc_mappings}@zip"
            break
    }

    // Legacy Dependencies, based on Protocol Version
    if ("${isLegacy}".toBoolean()) {
        int protocol = "${mc_protocol}".toInteger()
        if (protocol <= 78) { // MC 1.6.4 and below
            shadow(implementation('commons-codec:commons-codec:1.15'))
        }
        if (protocol <= 61) { // MC 1.5.2 and below
            shadow(implementation('org.apache.commons:commons-lang3:3.8.1'))
            shadow(implementation('commons-io:commons-io:2.5'))
            shadow(implementation('com.google.code.gson:gson:2.8.0'))
        }
        if (protocol <= 51) { // MC 1.4.7 and below
            shadow(implementation('com.google.guava:guava:14.0-rc3'))
        }
    }

    implementation "com.google.code.findbugs:jsr305:3.0.2"
}

jar {
    from sourceSets.main.output
}

remapJar {
    targetNamespace = "official"
    fallbackFromNamespace = "searge"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.shadow.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = 'UTF-8'

    // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
    // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
    // We'll use that if it's available, but otherwise we'll use the older option.
    if (targetVersion.isJava9Compatible()) {
        it.options.release = Integer.parseInt(targetVersion.getMajorVersion())
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version.toString()
    inputs.property "mcversion", "${mc_version}"

    // replace stuff in mcmod.info, nothing else
    filesMatching("mcmod.info") {
        // replace version and mcversion
        expand 'version': project.version.toString(),
                'mcversion': "${mc_version}"
    }

    // replace stuff in mods.toml, nothing else
    filesMatching("META-INF/mods.toml") {
        // replace version and mcversion
        expand 'version': project.version.toString(),
                'mcversion': "${mc_version}"
    }

    // replace stuff in mods.toml, nothing else
    filesMatching("fabric.mod.json") {
        // replace version and mcversion
        expand 'version': project.version.toString(),
                'mcversion': "${mc_version}"
    }

    // replace stuff in mod_modId.info, nothing else
    filesMatching("mod_${mod_name}.info") {
        // replace version
        expand 'version': project.version.toString()
    }

    filesMatching("mappings.srg") {
        filter { line ->
            if (line.startsWith("CL:")) {
                line
            } else {
                null
            }
        }
    }
}

def copyDir = "$compileJava.temporaryDir/replaced"
task processSource(type: Sync) {
    // This will ensure that this task is redone when the properties change.
    inputs.property "modName", "${mod_name}"
    inputs.property "versionId", "${versionId}"
    inputs.property "versionType", "${deploymentType}"
    inputs.property "versionLabel", "${versionLabel}"
    inputs.property "mcVersion", "${mc_version}"
    inputs.property "mcProtocol", "${mc_protocol}"

    from sourceSets.main.java
    filter(ReplaceTokens, tokens: [
            MOD_NAME     : mod_name,
            VERSION_ID   : versionId,
            VERSION_TYPE : deploymentType,
            VERSION_LABEL: versionLabel,
            MC_VERSION   : mc_version,
            MC_PROTOCOL  : mc_protocol
    ])
    into copyDir
}

compileJava {
    source = processSource.outputs
}
